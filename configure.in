dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sqlwork, 5.0)
AM_CONFIG_HEADER(config.h)

dnl Compilation environment is POSIX 1995 with extensions
CPPFLAGS="-D_POSIX_C_SOURCE=199506L -D__EXTENSIONS__"

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AM_PROG_LEX
AC_CHECK_HEADERS(getopt.h)
AC_CHECK_FUNCS(scandir)
AC_CHECK_FUNCS(alphasort)

AM_PATH_GTK(1.2.0, ,
            AC_MSG_ERROR(Cannot find GTK: Is gtk-config in path?))

dnl Add the languages which your application supports here.
ALL_LINGUAS=""
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}")
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}")

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl Check to see if the user wishes to support queries on
dnl Oracle databases
AC_ARG_ENABLE(oracle,
      [  --disable-oracle          Do not support Oracle databases],
      USE_ORACLE=$enableval, USE_ORACLE=yes)

AC_ARG_WITH(oracle, 
      [  --with-oracle             Oracle home directory located in (val)],
       ORACLE_DIR="$withval", ORACLE_DIR=no)

if test x"$USE_ORACLE" = x"yes" ; then
    AC_MSG_CHECKING([for Oracle 8.0 or 8.1])
    
    if test "${ORACLE_DIR}" != "no" && test -d $ORACLE_DIR/lib && 
       test -f $ORACLE_DIR/rdbms/demo/oci.h; then
	 ORACLE_DIR="$ORACLE_DIR"
    elif test -n $ORACLE_HOME && test -d $ORACLE_HOME/lib &&
       test -f $ORACLE_HOME/rdbms/demo/oci.h; then
	 ORACLE_DIR="$ORACLE_HOME"
    else
      AC_MSG_ERROR([Could not locate Oracle libraries!])
    fi
    AC_MSG_RESULT($ORACLE_DIR)
    DBINC="${DBINC} -I${ORACLE_DIR}/rdbms/demo -I${ORACLE_DIR}/network/public"
    if test -d ${ORACLE_DIR}/rdbms/public ; then
        DBINC="${DBINC} -I${ORACLE_DIR}/rdbms/public"
    fi
    DBSRC="${DBSRC} oracledb.c oracle.h"

    # Get Oracle version
    ORACLE_VERSION='Unknown'
changequote(,)dnl
    if test -x ${ORACLE_DIR}/bin/sqlplus; then
        tmpstr=`${ORACLE_DIR}/bin/sqlplus < /dev/null | awk '/Release/'`
    elif test -x ${ORACLE_DIR}/bin/svrmgrl; then
        tmpstr=`echo exit | TWO_TASK="" ${ORACLE_DIR}/bin/svrmgrl | awk '/^Oracle8/'`
    fi
    for vers in $tmpstr; do
        if test "`echo $vers | grep '^[0-9]'`" != ""  ; then
            ORACLE_VERSION="$vers"
            break
        fi
    done
    if test $ORACLE_VERSION = 'Unknown'; then
        tmpstr=`strings $ORACLE_HOME/lib/libclntsh.so|grep '^V8\.'`
        if test "$tmpstr" != ""  ; then
            ORACLE_VERSION="${tmpstr#V}"
        fi
    fi
changequote([,])dnl

    case ${ORACLE_VERSION} in
	8.1.* ) ;;
	8.0.* ) DBLIBS="${DBLIBS} -L${ORACLE_DIR}/lib ${ORACLE_DIR}/lib/scorept.o -lclntsh -lcore4 -lnlsrtl3" 
		DBINC="${DBINC} -I${ORACLE_DIR}/plsql/public" 
		;;
        *     ) AC_MSG_WARN([Unsupported Oracle version ${ORACLE_VERSION}!])
		;;
    esac
    DBDEFS="${DBDEFS} -DORACLE_DB -DORACLE_VERSION="`echo $ORACLE_VERSION | awk -F. '{printf "%d%02d%02d", $1, $2, $3}'`
fi

AC_ARG_WITH(mysql, 
      [  --with-mysql             Mysql home directory located in (val)],
       MYSQL_DIR="$withval", MYSQL_DIR=no)

AC_ARG_ENABLE(mysql,
      [  --disable-mysql          Do not support Mysql databases],
      USE_MYSQL=$enableval, USE_MYSQL=yes)

if test x"$USE_MYSQL" = x"yes" ; then
    AC_MSG_CHECKING([for Mysql headers])
    
    if test "${MYSQL_DIR}" != "no" && test -d $MYSQL_DIR/include/mysql && 
       test -f $MYSQL_DIR/include/mysql/mysql.h; then
	 MYSQL_DIR="$MYSQL_DIR"
    elif test -d /usr/include/mysql &&
       test -f /usr/include/mysql/mysql.h; then
	 MYSQL_DIR=/usr
    else
      AC_MSG_ERROR([Could not locate MySQL libraries!])
    fi
    AC_MSG_RESULT($MYSQL_DIR/include/mysql/mysql.h)

    DBSRC="${DBSRC} mysqldb.c"
    case $MYSQL_DIR in
	/usr ) ;;
	*    ) DBINC="${DBINC} -I${MYSQL_DIR}/include"
	       ;;
    esac
    DBDEFS="${DBDEFS} -DMYSQL_DB"
fi

AC_SUBST(LEXLIB)
AC_SUBST(DBINC)
AC_SUBST(DBLIBS)
AC_SUBST(DBSRC)
AC_SUBST(DBDEFS)

AC_OUTPUT([
Makefile
src/Makefile
intl/Makefile
po/Makefile.in
])

